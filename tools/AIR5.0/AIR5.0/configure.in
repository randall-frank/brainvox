dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/do_overlay_mask.c)

dnl Set up to put the configuration data into config.h
AC_CONFIG_HEADER(src/config.h)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_RANLIB
AC_PROG_MAKE_SET


AC_DEFINE(AIR_CONFIG_PATHS,1,[Define as 1 for UNIX, 2 for PC])
AC_DEFINE(AIR_CONFIG_AUTO_BYTESWAP,1,[Define as 1 unless non-compliant with ISO/IEEE Std 754-1985])
AC_DEFINE(AIR_CONFIG_OUTBITS,16,[Define as 16 unless you want an 8 bit version of AIR, in which case define as 8])

AC_DEFINE(AIR_CONFIG_REP16,1,[Relevant only when AIR_CONFIG_OUTBITS is 16, can be 1, 2 or 3])
AC_DEFINE(AIR_CONFIG_THRESHOLD1,7000,[Should be less than 256 if AIR_CONFIG_OUTBITS is 8])
AC_DEFINE(AIR_CONFIG_THRESHOLD2,7000,[Should be less than 256 is AIR_CONFIG_OUTBITS is 8])
AC_DEFINE(AIR_CONFIG_VERBOSITY,0,[Define as 1 if nonessential screen printing is desired])
AC_DEFINE(AIR_CONFIG_PIX_SIZE_ERR, .0001, [voxel sizes differing by less than this amount are assumed identical])



dnl See if gunzip is available
AC_CHECK_PROG(CONFIG_DECOMPRESS, gunzip, 1, 0)
AC_PATH_PROG(CONFIG_DECOMPRESS_COMMAND, gunzip, 0)

dnl Checks for libraries.
AC_CHECK_LIB(m,cos)

CONFIG_REQ_PERMS=2

dnl Checks for header files.
AC_HEADER_STDC

if test $ac_cv_header_stdc
then
	:
else
	AC_MSG_ERROR([AIR requires an ANSI C (ISO C) compliant compiler])
fi


AC_CHECK_HEADER(unistd.h,,unset CONFIG_REQ_PERMS)
AC_CHECK_HEADER(sys/types.h,,unset CONFIG_REQ_PERMS)
AC_CHECK_HEADER(sys/stat.h,,unset CONFIG_REQ_PERMS)


dnl Checks for typedefs, structures, and compiler characteristics.


dnl Checks for library functions.

CONFIG_PIPES=1

AC_CHECK_FUNC(popen,,unset CONFIG_PIPES)
AC_CHECK_FUNC(pclose,,unset CONFIG_PIPES)

if test $CONFIG_PIPES
then
	AC_DEFINE(AIR_CONFIG_PIPES,1,[MACHINE GENERATED, non-zero if popen() and pclose() were found])
fi

AC_DEFINE_UNQUOTED(AIR_CONFIG_DECOMPRESS,$CONFIG_DECOMPRESS,[MACHINE GENERATED, non-zero if gunzip was found])

if test $CONFIG_DECOMPRESS_COMMAND = 0
then
	:
else
	CONFIG_DECOMPRESS_COMMAND=\"${CONFIG_DECOMPRESS_COMMAND}\ -c\ \"
	AC_DEFINE_UNQUOTED(AIR_CONFIG_DECOMPRESS_COMMAND,$CONFIG_DECOMPRESS_COMMAND,[MACHINE GENERATED])
fi
AC_CHECK_FUNC(stat,,unset CONFIG_REQ_PERMS)

if test $CONFIG_REQ_PERMS
then
	if test $CONFIG_REQ_PERMS = 2
	then
		AC_CHECK_FUNC(lstat,,unset CONFIG_REQ_PERMS)
	fi
fi
if test $CONFIG_REQ_PERMS
then
	AC_DEFINE_UNQUOTED(AIR_CONFIG_REQ_PERMS,$CONFIG_REQ_PERMS,[MACHINE GENERATED, 1 if unistd.h, sys/types.h, sys/stat.h and stat() were found, 2 if lstat() was also found])
fi



AC_OUTPUT(src/Makefile Makefile)
